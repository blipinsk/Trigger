ext {
    configuration = [
            package          : "com.bartoszlipinski.trigger",
            compileVersion   : 23,
            buildToolsVersion: "23.0.2",
            minSdk           : 1,
            targetSdk        : 23,
            versionCode      : 0,
            versionName      : "0.0.0"
    ]

    bintrayConfig = [
            userOrg    : "blipinsk",
            userName   : "Bartosz Lipinski",
            userEmail  : "bartoszlipinski90@gmail.com",
            groupId    : "com.bartoszlipinski",
            name       : "Trigger",
            artifact   : "trigger",
            description: "Simple command pattern implementation",
            website    : "https://github.com/blipinsk/Trigger",
            gitUrl     : "https://github.com/blipinsk/Trigger.git",
            licenses   : ["Apache-2.0"],
            licenseDesc: "The Apache Software License, Version 2.0"
    ]

    bintrayUser = BINTRAY_USERNAME
    bintrayKey = BINTRAY_API_KEY
    dryRun = 'false'
    isUploading = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.novoda:bintray-release:0.3.4'
        // clean build bintrayUpload
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

subprojects {
    repositories {
        jcenter()
    }

    group = bintrayConfig.groupId
    version = configuration.versionName

    if (isUploading && project.name in [bintrayConfig.artifact]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name project.name
                        artifactId project.name
                        packaging 'aar'
                        description bintrayConfig.description
                        url bintrayConfig.website
                        version configuration.versionName

                        scm {
                            url bintrayConfig.gitUrl
                            connection bintrayConfig.gitUrl
                            developerConnection bintrayConfig.gitUrl
                        }

                        licenses {
                            license {
                                name bintrayConfig.licenseDesc
                            }
                        }

                        developers {
                            developer {
                                id bintrayConfig.userOrg
                                name bintrayConfig.userName
                                email bintrayConfig.userEmail
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
